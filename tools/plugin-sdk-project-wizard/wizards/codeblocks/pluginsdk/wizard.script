////////////////////////////////////////////////////////////////////////////////
// Plugin-SDK project wizard
////////////////////////////////////////////////////////////////////////////////

gGameSA <- false;
gGameVC <- false;
gGameIII <- false;
gPluginName <- _T("");
gClassName <- _T("");
gPluginType <- 0; // 0 - ASI Plugin, 1 - CLEO Plugin, 2 - MoonLoader Module, 3 - DLL Library
gDirectX <- false;
gLACompatibility <- false;
gOutputDirectory <- _T("");
gReleaseIncludeDirs <- _T("");
gReleaseLibraryDirs <- _T("");
gReleaseDependencies <- _T("");
gReleaseDefinitions <- _T("");
gDebugSettingsSameAsRelease <- true;
gDebugIncludeDirs <- _T("");
gDebugLibraryDirs <- _T("");
gDebugDependencies <- _T("");
gDebugDefinitions <- _T("");
gPluginSdkDir <- _T("");
gGTASADir <- _T("");
gGTAVCDir <- _T("");
gGTA3Dir <- _T("");
gCleoSdkGTASADir <- _T("");
gCleoSdkGTAVCDir <- _T("");
gCleoSdkGTA3Dir <- _T("");
gMoonLoaderSdkGTASADir <- _T("");
gDirectXDir <- _T("");
gRwD3d9Dir <- _T("");

function BeginWizard() {
    Wizard.AddProjectPathPage();
    Wizard.AddPage(_T("basicSetup"));
    Wizard.AddPage(_T("releaseSettings"));
    Wizard.AddPage(_T("debugSettings"));
    Wizard.AddPage(_T("pathsSetup"));
}

function IsLibraryExtension(ext) {
    local extLower = ext.Lower();
    return extLower.Matches(_T("asi")) ||
           extLower.Matches(_T("cleo")) ||
           extLower.Matches(_T("flt")) ||
           extLower.Matches(_T("dll")) ||
           extLower.Matches(_T("sf"));
}

function GetFileNameExtension(filename) {
    local lastDotPosition = -1;
    for (local i = 0; i < filename.Length(); i++) {
        if (filename.GetChar(i) == '.')
            lastDotPosition = i;
    }
    if (lastDotPosition != -1) {
        local extensionLength = filename.Length() - lastDotPosition - 1;
        if (extensionLength > 0)
            return filename.Mid(lastDotPosition, extensionLength);
    }
    return _T("");
}

function RemoveTrailingSpaces(str) {
    local len = str.Length();
    if (len > 0) {
        local strEndPos = len;
        for (local i = len - 1; i >= 0; i -= 1) {
            local c = str.GetChar(i);
            if (c == ' ' || c == '\t')
                strEndPos = i;
            else
                return str.Mid(0, strEndPos);
        }
    }
    return _T("");
}

function IsQuotesStartOrEnd(line, pos) {
    if (line.GetChar(pos) == '"') {
        if (pos > 0) {
            local slashesCount = 0;
            for (local i = pos - 1; i >= 0; i -= 1) {
                local c = line.GetChar(i);
                if (c == '\\')
                    slashesCount += 1;
                else
                    return slashesCount % 2 == 0;
            }
            return slashesCount % 2 == 0;
        }
        return true;
    }
    return false;
}

function SplitListLine(line) {
    local entry = _T("");
    local ary = ::wxArrayString();
    local insideQuotes = false;
    for (local i = 0; i < line.Length(); i += 1) {
        local c = line.GetChar(i);
        if (!entry.IsEmpty()) {
            if (insideQuotes) {
                if (IsQuotesStartOrEnd(line, i)) {
                    insideQuotes = false;
                    entry += _T("\"\\\"");
                }
                else
                    entry.AddChar(c);
            }
            else {
                if (c == ';' || c == ',' || c == '\n' || c == '\r') {
                    ary.Add(RemoveTrailingSpaces(entry), 1);
                    entry = _T("");
                }
                else {
                    if (IsQuotesStartOrEnd(line, i)) {
                        insideQuotes = true;
                        entry += _T("\"\\\"");
                    }
                    else
                        entry.AddChar(c);
                }
            }
        }
        else {
            if (c != ';' && c != ',' && c != '\n' && c != '\r' && c != '\t' && c != ' ') {
                if (IsQuotesStartOrEnd(line, i)) {
                    insideQuotes = true;
                    entry += _T("\"\\\"");
                }
                else
                    entry.AddChar(c);
            }
        }
    }
    if (!entry.IsEmpty())
        ary.Add(RemoveTrailingSpaces(entry), 1);
    return ary;
}

function NormalisePath(path) {
    local newPath = path + _T("");
    newPath.Replace(_T("/"), _T("\\"), true);
    local len = newPath.Length();
    if (len > 0) {
        local c = newPath.GetChar(len - 1);
        if (c != '\\')
            newPath.AddChar('\\');
    }
    return newPath;
}

function FormatName(name, defaultValue, gameId, gameDir, outDirName, configName) {
    local newLine = name + _T("");
    if (newLine.IsEmpty())
        return defaultValue;
    newLine.Replace(_T("$projectname$"), GetFixedProjectName(Wizard.GetProjectName()), true);
    newLine.Replace(_T("$PROJECTNAME$"), GetFixedProjectName(Wizard.GetProjectName()), true);
    newLine.Replace(_T("$projectdir$"), _T("$(PROJECT_DIR)"), true);
    newLine.Replace(_T("$PROJECTDIR$"), _T("$(PROJECT_DIR)"), true);
    if (!configName.IsEmpty()) {
        newLine.Replace(_T("$config$"), configName);
        newLine.Replace(_T("$CONFIG$"), configName);
    }
    if (!gameId.IsEmpty()) {
        newLine.Replace(_T("$game$"), gameId, true);
        newLine.Replace(_T("$GAME$"), gameId, true);
        newLine.Replace(_T("$gamelow$"), gameId.Lower(), true);
        newLine.Replace(_T("$GAMELOW$"), gameId.Lower(), true);
        if (gameId.Matches(_T("SA"))) {
            newLine.Replace(_T("$gamename$"), _T("San Andreas"), true);
            newLine.Replace(_T("$GAMENAME$"), _T("San Andreas"), true);
            newLine.Replace(_T("$gamealt$"), _T("SA"), true);
            newLine.Replace(_T("$GAMEALT$"), _T("SA"), true);
            newLine.Replace(_T("$gamealtlow$"), _T("sa"), true);
            newLine.Replace(_T("$GAMEALTLOW$"), _T("sa"), true);
        }
        else if (gameId.Matches(_T("VC"))) {
            newLine.Replace(_T("$gamename$"), _T("Vice City"), true);
            newLine.Replace(_T("$GAMENAME$"), _T("Vice City"), true);
            newLine.Replace(_T("$gamealt$"), _T("VC"), true);
            newLine.Replace(_T("$GAMEALT$"), _T("VC"), true);
            newLine.Replace(_T("$gamealtlow$"), _T("vc"), true);
            newLine.Replace(_T("$GAMEALTLOW$"), _T("vc"), true);
        }
        else {
            newLine.Replace(_T("$gamename$"), _T("3"), true);
            newLine.Replace(_T("$GAMENAME$"), _T("3"), true);
            newLine.Replace(_T("$gamealt$"), _T("3"), true);
            newLine.Replace(_T("$GAMEALT$"), _T("3"), true);
            newLine.Replace(_T("$gamealtlow$"), _T("3"), true);
            newLine.Replace(_T("$GAMEALTLOW$"), _T("3"), true);
        }
    }
    if (!gameDir.IsEmpty()) {
        newLine.Replace(_T("$gamedir$"), gameDir, true);
        newLine.Replace(_T("$GAMEDIR$"), gameDir, true);
    }
    if (!outDirName.IsEmpty()) {
        newLine.Replace(_T("$outdirname$"), outDirName, true);
        newLine.Replace(_T("$OUTDIRNAME$"), outDirName, true);
    }
    return newLine;
}

function GetConfigName(isDebug) {
    if (isDebug)
        return _T("Debug");
    return _T("Release");
}

function AddAdditionalToTarget(target, isDebug, gameAbbr, gameDir, outDirName, includeDirs, libraryDirs, libraries, defines) {
    local ary = SplitListLine(includeDirs);
    for (local i = 0; i < ary.GetCount(); i += 1)
        target.AddIncludeDir(FormatName(ary.Item(i), _T(""), gameAbbr, gameDir, outDirName, GetConfigName(isDebug)));
    ary = SplitListLine(libraryDirs);
    for (local i = 0; i < ary.GetCount(); i += 1)
        target.AddLibDir(FormatName(ary.Item(i), _T(""), gameAbbr, gameDir, outDirName, GetConfigName(isDebug)));
    ary = SplitListLine(libraries);
    for (local i = 0; i < ary.GetCount(); i += 1)
        target.AddLinkLib(FormatName(ary.Item(i), _T(""), gameAbbr, gameDir, outDirName, GetConfigName(isDebug)));
    ary = SplitListLine(defines);
    for (local i = 0; i < ary.GetCount(); i += 1)
        target.AddCompilerOption(_T("-D") + FormatName(ary.Item(i), _T(""), gameAbbr, gameDir, outDirName, GetConfigName(isDebug)));
}

function CreateNewBuildTarget(project, targetName, gameId, isDebug) {
    local gameAbbr = gameId.Upper();
    local gameAbbrLow = gameId.Lower();
    local baseTargetName = Wizard.GetReleaseName(); 
    if (isDebug)
        baseTargetName = Wizard.GetDebugName();
    local target = project.DuplicateBuildTarget(baseTargetName, targetName);
    if (IsNull(target))
        return;
    target.SetTargetType(ttDynamicLib);
    target.SetCreateStaticLib(false);
    target.SetCreateDefFile(false);
    target.SetWorkingDir(_T("."));
    target.SetObjectOutput(_T("obj"));
    target.SetTargetFilenameGenerationPolicy(tgfpNone, tgfpNone);
    target.AddCompilerOption(_T("-m32"));
    target.AddCompilerOption(_T("-fpermissive"));
    local configName = GetConfigName(isDebug);
    if (isDebug) {
        DebugSymbolsOn(target, Wizard.GetCompilerID());
        target.AddCompilerOption(_T("-D_DEBUG"));
    }
    else {
        OptimizationsOn(target, Wizard.GetCompilerID());
        target.AddCompilerOption(_T("-DNDEBUG"));
    }
    local gameDir;
    local outDirName;
    if (gameAbbr.Matches(_T("SA")))
        gameDir = gGTASADir + _T("");
    else if (gameAbbr.Matches(_T("VC")))
        gameDir = gGTAVCDir + _T("");
    else
        gameDir = gGTA3Dir + _T("");
    
    if (gPluginType == 0) // ASI Plugin
        outDirName = _T("scripts");
    else if (gPluginType == 1) // CLEO Plugin
        outDirName = _T("cleo");
    else if (gPluginType == 2) // MoonLoader Module
        outDirName = _T("moonloader\\lib");
    else // DLL Library
        outDirName = _T("");
    local outputDir = FormatName(gOutputDirectory, _T("$(PROJECT_DIR)\\output"), gameAbbr, gameDir, outDirName, configName);
    outputDir = NormalisePath(outputDir);
    local targetName = FormatName(gPluginName, GetFixedProjectName(Wizard.GetProjectName()), gameAbbr, _T(""), _T(""), configName);
    local targetExt;
    local fileExt = GetFileNameExtension(targetName);
    if (!fileExt.IsEmpty() && IsLibraryExtension(fileExt)) {
        targetExt = fileExt + _T("");
        targetName = targetName.Mid(0, targetName.Length() - fileExt.Length());
    }
    else {
        if (gPluginType == 1) // CLEO Plugin
            targetExt = _T("cleo");
        else if (gPluginType == 2 || gPluginType == 3) // MoonLoader Module or DLL Library
            targetExt = _T("dll");
        else // ASI Plugin
            targetExt = _T("asi");
    }
    if (isDebug)
        targetName += _T("_d");
    target.SetOutputFilename(outputDir + targetName + _T(".") + targetExt);
    local pluginSdkDir = FormatName(gPluginSdkDir, _T("$(PLUGIN_SDK_DIR)"), gameAbbr, gameDir, outDirName, configName);
    pluginSdkDir = NormalisePath(pluginSdkDir);
    target.AddIncludeDir(pluginSdkDir + _T("plugin_") + gameAbbrLow + _T("\\"));
    target.AddIncludeDir(pluginSdkDir + _T("plugin_") + gameAbbrLow + _T("\\game_") + gameAbbrLow + _T("\\"));
    target.AddLibDir(pluginSdkDir + _T("output\\mingw\\lib\\"));
    local pathsLibName = _T("paths");
    local pluginLibName = _T("plugin");
    if (!gameAbbr.Matches(_T("SA")))
        pluginLibName += _T("_") + gameAbbrLow;
    if (isDebug) {
        pathsLibName += _T("_d");
        pluginLibName += _T("_d");
    }
    target.AddLinkLib(pathsLibName);
    target.AddLinkLib(pluginLibName);
    if (gameAbbr.Matches(_T("SA"))) {
        target.AddCompilerOption(_T("-DGTASA"));
        target.AddCompilerOption(_T("-DGTAGAME_NAME=\"\\\"San Andreas\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_ABBR=\"\\\"SA\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_ABBRLOW=\"\\\"sa\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_PROTAGONISTNAME=\"\\\"CJ\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_CITYNAME=\"\\\"San Andreas\"\\\""));
    }
    else if (gameAbbr.Matches(_T("VC"))) {
        target.AddCompilerOption(_T("-DGTAVC"));
        target.AddCompilerOption(_T("-DGTAGAME_NAME=\"\\\"Vice City\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_ABBR=\"\\\"VC\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_ABBRLOW=\"\\\"vc\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_PROTAGONISTNAME=\"\\\"Tommy\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_CITYNAME=\"\\\"Vice City\"\\\""));
    }
    else {
        target.AddCompilerOption(_T("-DGTA3"));
        target.AddCompilerOption(_T("-DGTAGAME_NAME=\"\\\"3\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_ABBR=\"\\\"3\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_ABBRLOW=\"\\\"3\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_PROTAGONISTNAME=\"\\\"Claude\"\\\""));
        target.AddCompilerOption(_T("-DGTAGAME_CITYNAME=\"\\\"Liberty City\"\\\""));
    }
    if (gDirectX) {
        local dxSdkDir = FormatName(gDirectXDir, _T("$(DIRECTX9_SDK_DIR)"), gameAbbr, gameDir, outDirName, configName);
        dxSdkDir = NormalisePath(dxSdkDir);
        target.AddIncludeDir(dxSdkDir + _T("Include\\"));
        target.AddLibDir(dxSdkDir + _T("Lib\\x86\\"));
        target.AddLinkLib(_T("d3d9"));
        target.AddLinkLib(_T("d3dx9"));
        if (gameAbbr.Matches(_T("VC")) || gameAbbr.Matches(_T("III"))) {
            local rwD3D9Dir = FormatName(gRwD3d9Dir, _T("$(RWD3D9_DIR)"), gameAbbr, gameDir, outDirName, configName);
            rwD3D9Dir = NormalisePath(rwD3D9Dir);
            target.AddIncludeDir(rwD3D9Dir + _T("source\\"));
            target.AddLibDir(rwD3D9Dir + _T("libs\\"));
            target.AddLinkLib(_T("rwd3d9"));
        }
    }
    if (gPluginType == 1) { // CLEO Plugin
        local cleoSdkDir = FormatName(gCleoSdkGTASADir, _T("$(CLEO_SDK_") + gameAbbr + _T("_DIR)"), gameAbbr, gameDir, outDirName, configName);
        cleoSdkDir = NormalisePath(cleoSdkDir);
        target.AddIncludeDir(cleoSdkDir);
        target.AddLibDir(cleoSdkDir);
        if (gameAbbr.Matches(_T("SA")))
            target.AddLinkLib(_T("cleo"));
        else
            target.AddLinkLib(gameAbbr + _T(".CLEO"));
    }
    else if (gPluginType == 2) { // MoonLoader Module
        local moonloaderSdkDir = FormatName(gMoonLoaderSdkGTASADir, _T("$(MOONLOADER_SDK_SA_DIR)"), gameAbbr, gameDir, outDirName, configName);
        moonloaderSdkDir = NormalisePath(moonloaderSdkDir);
        target.AddIncludeDir(moonloaderSdkDir + _T("src\\"));
        target.AddIncludeDir(moonloaderSdkDir + _T("src\\libs\\lua\\"));
        target.AddIncludeDir(moonloaderSdkDir + _T("src\\libs\\sol2\\"));
        target.AddLibDir(moonloaderSdkDir + _T("src\\libs\\lua\\"));
        target.AddLinkLib(_T("lua51"));
        target.AddCompilerOption(_T("-DLUA_BUILD_AS_DLL"));
        target.AddCompilerOption(_T("-DMODULE_NAME=\"") + GetFixedProjectName(Wizard.GetProjectName()) + _T("\""));
    }
    if (gLACompatibility)
        target.AddCompilerOption(_T("-D_PLUGIN_LA_COMP"));
    if (!isDebug || gDebugSettingsSameAsRelease)
        AddAdditionalToTarget(target, isDebug, gameAbbr, gameDir, outDirName, gReleaseIncludeDirs, gReleaseLibraryDirs, gReleaseDependencies, gReleaseDefinitions);
    else
        AddAdditionalToTarget(target, isDebug, gameAbbr, gameDir, outDirName, gDebugIncludeDirs, gDebugLibraryDirs, gDebugDependencies, gDebugDefinitions);
}

function SetupProject(project) {
    if (gGameSA) {
        CreateNewBuildTarget(project, _T("GTASA Release"), _T("SA"), false);
        CreateNewBuildTarget(project, _T("GTASA Debug"), _T("SA"), true);
    }
    if (gGameVC) {
        CreateNewBuildTarget(project, _T("GTAVC Release"), _T("VC"), false);
        CreateNewBuildTarget(project, _T("GTAVC Debug"), _T("VC"), true);
    }
    if (gGameIII) {
        CreateNewBuildTarget(project, _T("GTA3 Release"), _T("III"), false);
        CreateNewBuildTarget(project, _T("GTA3 Debug"), _T("III"), true);
    }
    project.RemoveBuildTarget(Wizard.GetDebugName());
    project.RemoveBuildTarget(Wizard.GetReleaseName());
    if (gPluginType == 2) { // MoonLoader Module
        local pchfile = project.GetFileByFilename(_T("pch.h"), true, true);
        if (!IsNull(pchfile)) {
            pchfile.compile = true;
            pchfile.link = false;
            pchfile.weight = 0;
            project.SetModeForPCH(pchSourceDir);
            project.AddCompilerOption(_T("-Winvalid-pch"));
            project.AddCompilerOption(_T("-include pch.h"));
        }
    }
    return true;
}

function GetCodeFromFile(filepath, projname, className, instanceName) {
    local code = IO.ReadFileContents(Wizard.FindTemplateFile(filepath));
    code.Replace(_T("$projname$"), projname);
    code.Replace(_T("$classname$"), className);
    code.Replace(_T("$classinstancename$"), instanceName);
    return code;
}

function GetGeneratedFile(file_index) {
    local projname = GetFixedProjectName(Wizard.GetProjectName());
    local className = FormatName(gClassName, projname, _T(""), _T(""), _T(""), _T(""));
    className = GetFixedProjectName(className);
    local instanceName;
    if (className.GetChar(0) >= 'A' && className.GetChar(0) <= 'Z') {
        local lowerLetter = _T("");
        lowerLetter.AddChar(className.GetChar(0));
        lowerLetter = lowerLetter.Lower();
        instanceName = lowerLetter + className.Mid(1, className.Length() - 1);
    }
    else {
        if (className.GetChar(0) == '_')
            instanceName = className + _T("_inst");
        else
            instanceName = _T("_") + className;
    }
    if (file_index == 0) {
        local path;
        if (gPluginType == 1 && gGameSA && !gGameVC && !gGameIII) // CLEO Plugin
            path = _T("pluginsdk/files/Main.Cleo.SA.cpp");
        else if (gPluginType == 2 && gGameSA && !gGameVC && !gGameIII) // MoonLoader Module
            path = _T("pluginsdk/files/Main.MoonLoader.SA.cpp");
        else
            path = _T("pluginsdk/files/Main.Asi.cpp");
        return projname + _T(".cpp;") + GetCodeFromFile(path, projname, className, instanceName);
    }
    else if (file_index == 1 && gPluginType == 2) // MoonLoader Module
        return _T("pch.h;") + GetCodeFromFile(_T("pluginsdk/files/Pch.MoonLoader.h"), projname, className, instanceName);
    return _T("");
}

////////////////////////////////////////////////////////////////////////////////
// Basic setup page
////////////////////////////////////////////////////////////////////////////////

function AnyGameChecked() {
    return Wizard.IsCheckboxChecked(_T("chkGTASA")) || Wizard.IsCheckboxChecked(_T("chkGTAVC")) || Wizard.IsCheckboxChecked(_T("chkGTA3"));
}

function BasicSetupSwitchComponents(enable) {
    Wizard.EnableWindow(_T("sttPluginType"), enable);
    Wizard.EnableWindow(_T("cmbPluginType"), enable);
    Wizard.EnableWindow(_T("sttPluginName"), enable);
    Wizard.EnableWindow(_T("txtPluginName"), enable);
    Wizard.EnableWindow(_T("sttClassName"), enable);
    Wizard.EnableWindow(_T("txtClassName"), enable);
    Wizard.EnableWindow(_T("chkCleoPlugin"), enable);
    Wizard.EnableWindow(_T("chkCleoSdk"), enable);
    Wizard.EnableWindow(_T("chkUseDirectX"), enable);
    Wizard.EnableWindow(_T("chkLACompatibility"), enable);
    Wizard.EnableWindow(_T("sttOutputDirectory"), enable);
    Wizard.EnableWindow(_T("txtOutputDirectory"), enable);
    Wizard.EnableWindow(_T("btnGameFolder"), enable);
    Wizard.EnableWindow(_T("btnProjectFolder"), enable);
}

function BasicSetupCheckForComponentsSwitch(thisCheckbox, secondCheckbox, thirdCheckbox) {
    if (Wizard.IsCheckboxChecked(thisCheckbox)) {
        if (!Wizard.IsCheckboxChecked(secondCheckbox) && !Wizard.IsCheckboxChecked(thirdCheckbox))
            BasicSetupSwitchComponents(true);
    }
    else {
        if (!Wizard.IsCheckboxChecked(secondCheckbox) && !Wizard.IsCheckboxChecked(thirdCheckbox))
            BasicSetupSwitchComponents(false);
    }
}

function OnClick_chkGTASA() {
    BasicSetupCheckForComponentsSwitch(_T("chkGTASA"), _T("chkGTAVC"), _T("chkGTA3"));
}

function OnClick_chkGTAVC() {
    if (Wizard.GetComboboxSelection(_T("cmbPluginType")) == 2 && Wizard.IsCheckboxChecked(_T("chkGTAVC"))) {
        if (!Wizard.IsCheckboxChecked(_T("chkGTASA"))) {
            Wizard.SetComboboxSelection(_T("cmbPluginType"), 0);
            gPluginType = 0; // ASI Plugin
        }
        else {
            Wizard.CheckCheckbox(_T("chkGTAVC"), false);
            ShowWarning(_T("MoonLoader Module can't be created for GTA Vice City"));
        }
    }
    BasicSetupCheckForComponentsSwitch(_T("chkGTAVC"), _T("chkGTASA"), _T("chkGTA3"));
}

function OnClick_chkGTA3() {
    if (Wizard.GetComboboxSelection(_T("cmbPluginType")) == 2 && Wizard.IsCheckboxChecked(_T("chkGTA3"))) {
        if (!Wizard.IsCheckboxChecked(_T("chkGTASA"))) {
            Wizard.SetComboboxSelection(_T("cmbPluginType"), 0);
            gPluginType = 0; // ASI Plugin
        }
        else {
            Wizard.CheckCheckbox(_T("chkGTA3"), false);
            ShowWarning(_T("MoonLoader Module can't be created for GTA 3"));
        }
    }
    BasicSetupCheckForComponentsSwitch(_T("chkGTA3"), _T("chkGTASA"), _T("chkGTAVC"));
}

function OnClick_cmbPluginType() {
    local selectedItemId = Wizard.GetComboboxSelection(_T("cmbPluginType"));
    if (selectedItemId == 2 && !Wizard.IsCheckboxChecked(_T("chkGTASA"))) {
        Wizard.SetComboboxSelection(_T("cmbPluginType"), gPluginType);
        ShowWarning(_T("MoonLoader Module can be created for GTA San Andreas only"));
    }
    else if (selectedItemId == 2 && (Wizard.IsCheckboxChecked(_T("chkGTAVC")) || Wizard.IsCheckboxChecked(_T("chkGTA3")))) {
        Wizard.SetComboboxSelection(_T("cmbPluginType"), gPluginType);
        local errMessage = _T("MoonLoader Module can't be created for ");
        if (Wizard.IsCheckboxChecked(_T("chkGTA3")))
            errMessage += _T("GTA 3");
        if (Wizard.IsCheckboxChecked(_T("chkGTAVC"))) {
            if (Wizard.IsCheckboxChecked(_T("chkGTA3")))
                errMessage += _T(" and ");
            errMessage += _T("GTA Vice City");
        }
        ShowWarning(errMessage);
    }
    else
        gPluginType = selectedItemId;
}

function OnClick_btnGameFolder() {
    Wizard.SetTextControlValue(_T("txtOutputDirectory"), _T("$gamedir$\\$outdirname$"));
}

function OnClick_btnProjectFolder() {
    Wizard.SetTextControlValue(_T("txtOutputDirectory"), _T("$projectdir$\\output"));
}

function OnLeave_basicSetup(forward) {
    if (forward) {
        if (!AnyGameChecked()) {
            ShowWarning(_T("Please select target game(s)"));
            return false;
        }
        gPluginType = Wizard.GetComboboxSelection(_T("cmbPluginType"));
        if (forward && gPluginType == -1) {
            ShowWarning(_T("Please select plugin type"));
            return false;
        }
    }
    gGameSA = Wizard.IsCheckboxChecked(_T("chkGTASA"));
    gGameVC = Wizard.IsCheckboxChecked(_T("chkGTAVC"));
    gGameIII = Wizard.IsCheckboxChecked(_T("chkGTA3"));
    gPluginName = Wizard.GetTextControlValue(_T("txtPluginName"));
    gClassName = Wizard.GetTextControlValue(_T("txtClassName"));
    gDirectX = Wizard.IsCheckboxChecked(_T("chkUseDirectX"));
    gLACompatibility = Wizard.IsCheckboxChecked(_T("chkLACompatibility"));
    gOutputDirectory = Wizard.GetTextControlValue(_T("txtOutputDirectory"));
    return true;
}

////////////////////////////////////////////////////////////////////////////////
//
// Release settings page
//
////////////////////////////////////////////////////////////////////////////////

function OnLeave_releaseSettings(forward) {
    gReleaseIncludeDirs = Wizard.GetTextControlValue(_T("txtReleaseAdditionalIncludeDirs"));
    gReleaseLibraryDirs = Wizard.GetTextControlValue(_T("txtReleaseAdditionalLibraryDirs"));
    gReleaseDependencies = Wizard.GetTextControlValue(_T("txtReleaseAdditionalDependencies"));
    gReleaseDefinitions = Wizard.GetTextControlValue(_T("txtReleaseAdditionalPreprocessorDefinitions"));
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// Debug settings page
////////////////////////////////////////////////////////////////////////////////

function DebugSettingsCopyFromRelease() {
    Wizard.SetTextControlValue(_T("txtDebugAdditionalIncludeDirs"), gReleaseIncludeDirs);
    Wizard.SetTextControlValue(_T("txtDebugAdditionalLibraryDirs"), gReleaseLibraryDirs);
    Wizard.SetTextControlValue(_T("txtDebugAdditionalDependencies"), gReleaseDependencies);
    Wizard.SetTextControlValue(_T("txtDebugAdditionalPreprocessorDefinitions"), gReleaseDefinitions);
}

function DebugSettingsSwitchComponents(enable) {
    Wizard.EnableWindow(_T("sttDebugAdditionalIncludeDirs"), enable);
    Wizard.EnableWindow(_T("txtDebugAdditionalIncludeDirs"), enable);
    Wizard.EnableWindow(_T("sttDebugAdditionalLibraryDirs"), enable);
    Wizard.EnableWindow(_T("txtDebugAdditionalLibraryDirs"), enable);
    Wizard.EnableWindow(_T("sttDebugAdditionalDependencies"), enable);
    Wizard.EnableWindow(_T("txtDebugAdditionalDependencies"), enable);
    Wizard.EnableWindow(_T("sttDebugAdditionalPreprocessorDefinitions"), enable);
    Wizard.EnableWindow(_T("txtDebugAdditionalPreprocessorDefinitions"), enable);
}

function OnClick_chkDebugSettingsSameAsRelease() {
    if (Wizard.IsCheckboxChecked(_T("chkDebugSettingsSameAsRelease")))
        DebugSettingsSwitchComponents(false);
    else
        DebugSettingsSwitchComponents(true);
    DebugSettingsCopyFromRelease();
}

function OnEnter_debugSettings(forward) {
    if (forward && Wizard.IsCheckboxChecked(_T("chkDebugSettingsSameAsRelease")))
        DebugSettingsCopyFromRelease();
}

function OnLeave_debugSettings(forward) {
    gDebugSettingsSameAsRelease = Wizard.IsCheckboxChecked(_T("chkDebugSettingsSameAsRelease"));
    gDebugIncludeDirs = Wizard.GetTextControlValue(_T("txtDebugAdditionalIncludeDirs"));
    gDebugLibraryDirs = Wizard.GetTextControlValue(_T("txtDebugAdditionalLibraryDirs"));
    gDebugDependencies = Wizard.GetTextControlValue(_T("txtDebugAdditionalDependencies"));
    gDebugDefinitions = Wizard.GetTextControlValue(_T("txtDebugAdditionalPreprocessorDefinitions"));
    return true;
}

////////////////////////////////////////////////////////////////////////////////
// Setup paths page
////////////////////////////////////////////////////////////////////////////////

function PathsSetupSwitchComponentsForGame(enable, gameDirCompName, cleoSdkDirCompName) {
    if (enable) {
        local wantsGameDir = gOutputDirectory.Find(_T("$gamedir$")) != -1;
        Wizard.EnableWindow(_T("stt") + gameDirCompName, wantsGameDir);
        Wizard.EnableWindow(_T("txt") + gameDirCompName, wantsGameDir);
        Wizard.EnableWindow(_T("stt") + cleoSdkDirCompName, gPluginType == 1); // CLEO Plugin
        Wizard.EnableWindow(_T("txt") + cleoSdkDirCompName, gPluginType == 1); // CLEO Plugin
    }
    else {
        Wizard.EnableWindow(_T("stt") + gameDirCompName, false);
        Wizard.EnableWindow(_T("txt") + gameDirCompName, false);
        Wizard.EnableWindow(_T("stt") + cleoSdkDirCompName, false);
        Wizard.EnableWindow(_T("txt") + cleoSdkDirCompName, false);
    }
}

function PathsSetupSwitchMoonLoader(enable) {
    Wizard.EnableWindow(_T("sttMoonLoaderSdkDir"), enable);
    Wizard.EnableWindow(_T("txtMoonLoaderSdkDir"), enable);
}

function PathsSetupSwitchDirectX(enable) {
    Wizard.EnableWindow(_T("sttDirectXSdkDir"), enable);
    Wizard.EnableWindow(_T("txtDirectXSdkDir"), enable);
}

function PathsSetupSwitchRwD3D9(enable) {
    Wizard.EnableWindow(_T("sttRwD3d9Dir"), enable);
    Wizard.EnableWindow(_T("txtRwD3d9Dir"), enable);
}

function OnLeave_pathsSetup(forward) {
    gPluginSdkDir = Wizard.GetTextControlValue(_T("txtPluginSdkDir"));
    gGTASADir = Wizard.GetTextControlValue(_T("txtGtaSaAsiDir"));
    gGTAVCDir = Wizard.GetTextControlValue(_T("txtGtaVcAsiDir"));
    gGTA3Dir = Wizard.GetTextControlValue(_T("txtGta3AsiDir"));
    gCleoSdkGTASADir = Wizard.GetTextControlValue(_T("txtCleoSdkSaDir"));
    gCleoSdkGTAVCDir = Wizard.GetTextControlValue(_T("txtCleoSdkVcDir"));
    gCleoSdkGTA3Dir = Wizard.GetTextControlValue(_T("txtCleoSdk3Dir"));
    gMoonLoaderSdkGTASADir = Wizard.GetTextControlValue(_T("txtMoonLoaderSdkDir"));
    gDirectXDir = Wizard.GetTextControlValue(_T("txtDirectXSdkDir"));
    gRwD3d9Dir = Wizard.GetTextControlValue(_T("txtRwD3d9Dir"));
    return true;
}

function OnEnter_pathsSetup(forward) {
    PathsSetupSwitchComponentsForGame(gGameSA, _T("GtaSaDir"), _T("CleoSdkSaDir"));
    PathsSetupSwitchComponentsForGame(gGameVC, _T("GtaVcDir"), _T("CleoSdkVcDir"));
    PathsSetupSwitchComponentsForGame(gGameIII, _T("Gta3Dir"), _T("CleoSdk3Dir"));
    PathsSetupSwitchMoonLoader(gGameSA && gPluginType == 2); // MoonLoader Module
    PathsSetupSwitchDirectX(gDirectX);
    PathsSetupSwitchRwD3D9(gDirectX && (gGameVC || gGameIII));
}
